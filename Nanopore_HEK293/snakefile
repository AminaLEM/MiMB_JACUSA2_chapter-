configfile: "config.yaml"

##########################################################Global variables
out_analysis = "{path_out}/{label}/".format(**config)
out_jacusa = "{path_out}/{label}/jacusa/".format(**config)
out_bam = "{path_out}/{label}/bam/".format(**config)
out = config['path_out']

comb = config['pattern_params']

#Extract data from the dictionnary
data = config['data']
cond1= data['cond1']
cond2= data['cond2']
ground_truth = config['modified_sites']

#Java params
jarfile= config["jar"]    
java_params= config["java_params"]
javaparams = ""
for key in java_params:
      javaparams = javaparams + "-"+key+ " " +str(java_params[key])+ " "

#Combine all jacusa params in one string    
parameters= config["jacusa_params"]
allparams = ''
for key in parameters:
    if key == 'b':
        merged = touch(parameters[key]+".contigous_regions.bed")
        allparams = allparams + "-"+key+ " " +merged+ " "
    else: 
        allparams = allparams + "-"+key+ " " +str(parameters[key])+ " "
        
####################################################Rules

# Create index for bam file
rule sort_bam: 
    input:config['path_inp']+"/{sample}.bam"
    output: out_bam+"{sample}.sorted.bam"
    shell:
        """
        samtools sort {input} > {output}
        """
#Filter out secondary and poor alignments. 
rule filter_bam: 
    input: out_bam+"{sample}.sorted.bam"
    output: 
        bam = out_bam+"{sample}.filtered.bam",
        bai = out_bam+"{sample}.filtered.bam.bai"
        
    shell:
        """
        samtools view -F 3328 -b {input} > {output.bam}
        samtools index {output.bam} > {output.bai}
        """

#apply JACUSA call2 with replicates           
rule jacusa2_call2: 
    input:
       bam1=expand(out_bam+"{c1}.filtered.bam",c1=cond1),
       bam2= expand(out_bam+"{c2}.filtered.bam",c2=cond2)
    output: out_jacusa+"Cond1vsCond2Call2.out"
    run:
        input_str_bam1 = ''
        for i in input.bam1:
         input_str_bam1 = input_str_bam1+i+','
        input_str_bam1 = input_str_bam1[:-1]
        
        input_str_bam2 = ''
        for i in input.bam2:
         input_str_bam2 = input_str_bam2+i+','
        input_str_bam2 = input_str_bam2[:-1]
        
        shell( "java {javaparams} -jar {jarfile} call-2 {allparams} -r {output} {input_str_bam1} {input_str_bam2}")

# preprocess jacusa2 output
rule get_features: 
    input:  out_jacusa+"Cond1vsCond2Call2.out"
    #input: "/prj/MiMB_book_chapter_Amina_Isabel/Nanopore/HEK293/JACUSA2/WT_vs_KO_RC22_call2_result.out"
    output: 
        out = out_analysis +"features/features.rds",
        
    params: 
        mod = ground_truth,
        ref = config['reference'],
        chr_size = config['chr_size'],
        inter = out_analysis +"tmp", 
        sites = config['regions']
        
    shell: 
        """
        bash scripts/preprocessing.sh {input} {params.chr_size} {params.ref} {params.inter} {params.sites}
        Rscript scripts/get_features.R {params.inter} {output.out}
        """
# split data into test set and traning set  for internal pattern use case      
rule split_train_test:
    input: out_analysis +"features/features.rds"
    output: 
        train=out_analysis +"features/train_features.rds" ,
        test=out_analysis +"features/test_features.rds"    
    params: 
        int_pt = comb['internal_pattern']
    run:
        if comb['external_pattern'] != "":
            shell(" cp {input} {output.test}")
            shell("touch {output.train}")
        else:
            shell("Rscript scripts/split.R {input} {ground_truth} {output.train} {output.test} {params.int_pt}")
        
# Extract pattern or use an exernal pattern        
rule get_pattern: 
    input: out_analysis +"features/train_features.rds" 
    output: 
        data=out_analysis +"patterns/NMF.rds",
        rank1= out_analysis +"patterns/asses_NMF_1.pdf" ,       
        rank2= out_analysis +"patterns/asses_NMF_2.pdf"
    params: 
        mod = ground_truth,
        ext_pat =comb['external_pattern']
    run:
        if comb['external_pattern']!= "":
            shell(" cp {params.ext_pat} {output.data}")
            shell(" touch {output.rank1} {output.rank2}")

        else:
            shell("Rscript scripts/get_pattern.R {input} {output.data} {output.rank1} {output.rank2}")
            
# Visualize patterns        
rule visualize_pattern: 
    input: 
        nmf = out_analysis +"patterns/NMF.rds" 
    output: 
        data=directory(out_analysis +"patterns/viz")
    params: 
        mod = ground_truth,
        pattern = comb['combined_patterns']
    script: "scripts/pattern_viz.R"

# Perform prediction on test set
rule predict_modification: 
    input: 
        bigtable = out_analysis +"features/features.rds" ,
        nmf = out_analysis +"patterns/NMF.rds" 
    output: 
        data=directory(out_analysis +"prediction")
    params: 
        mod = ground_truth,
        pattern = comb['combined_patterns'],
        int_pt = comb['internal_pattern']
    script: "scripts/prediction.R"
